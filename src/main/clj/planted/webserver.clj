(ns planted.webserver
  (:use [org.httpkit.server :only [run-server]])
  (:require [clojure.tools.logging :as log]
            [com.stuartsierra.component :as component]
            [compojure.core :refer [routes ANY POST GET]]
            [compojure.route :as route]
            [liberator.core :refer [resource defresource get-options]]
            [ring.middleware.content-type :refer [wrap-content-type]]
            [ring.middleware.not-modified :refer [wrap-not-modified]]
            [ring.middleware.params :refer [wrap-params]]
            [ring.util.response :refer [content-type resource-response]]))

(defn log-request
  "Logs the request details, the start/end time and any exceptions"
  [handler]
  (fn [req]
    (log/info "Request start:" (:request-method req) (:uri req) (:query-string req))
    (log/trace "Request map:" (pr-str req))
    (try
      (let [res (handler req)]
        (log/info "Request end:" (:uri req) (:status res))
        res)
      (catch Throwable t
        (log/error t "Unhandled throwable")
        (throw t)))))

(defn setup-web-server
  "Defines the Planted web server, which includes the REST web services and
  serving of static html/js files.
  The static assets should have been composed beforehand via grunt."
  [db]
  (routes
    ;; TODO services here...
    ;; (ANY "/data/v1/:todo/:placeholder" [todo placeholder] ...

    ;; Static files are generated by the grunt build, serving them here is just
    ;; passing them through.
    ;; This call defaults to using "resources/public" folder as source
    (route/resources "/")

    ;; Planted is a single-page app. This means, whatever URL is actually
    ;; requested by the browser, we're always going to return the same
    ;; javascript app, which will internally detect the URL and render
    ;; appropriately. An easy way to redirect deep links to the single html
    ;; page is to simply capture anything that falls through from above.
    ;; The single page app itself can gracefully handle actual 404s.
    (route/not-found
      ;; TODO prerender the javascript app server side
      (content-type (resource-response "public/planted.html") "text/html"))))

(defn wrap-web-server
  "Initializes the application web request handling and initializes some other
  ring request handlers to enhance it."
  [db]
  (-> (setup-web-server db)

      ;; Ring middleware libraries to plug in some handy functionality
      wrap-not-modified
      wrap-content-type
      wrap-params

      ;; Log everything that comes in
      log-request))

;; See docstring for new-webserver below
(defrecord WebServer [bind db shutdown-method]
  component/Lifecycle

  (start [this]
    (log/info "Starting Planted web server on" bind)
    (if (not db)
      (throw "Web server component requires 'db' dependency be specified."))
    (if shutdown-method
      this
      ;; The method returned from "run-server" shuts down the instance
      (let [shutdown-method (run-server (wrap-web-server db) {:port bind})]
        (assoc this :shutdown-method shutdown-method))))

  (stop [this]
    (log/info "Stopping Planted web server")
    (if (not shutdown-method)
      this
      (do (try (shutdown-method :timeout 250)
            (catch Throwable t
              (log/warn t "Error when shutting down web server")))
          (assoc this :shutdown-method nil)))))

(defn new-webserver
  "This class implements the Lifecycle interface providing start/stop methods
  for the web server part of this application (including web services and
  static files.) This implementation of Lifecycle is idempotent.

  The 'bind' parameter specifies what port the server shall bind to.

  The returned Lifecycle record requires a 'db' parameter to be bound."
  [bind]
  (map->WebServer {:bind bind}))